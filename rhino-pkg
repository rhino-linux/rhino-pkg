#!/usr/bin/env nu
def translation-dir-path [] -> string {
    "/usr/src/pacstall/rhino-pkg/translation_tomls"
}

def cmd-exist [input: string] -> bool {
    let stuff = (which $input)
    if ($stuff | is-empty) { return false } else if ($stuff).type.0 == "external" { return true }
}

def search-apt [input: string, desc: bool] -> table {
    if (cmd-exist 'apt-cache') {
      let first_table =  if $desc == true {
            (^apt-cache search $input | lines | parse "{package} - {description}" | insert provider 'apt') 
        } else {
            (^apt-cache search --names-only $input | lines | parse "{package} - {description}" | insert provider 'apt') 
        } 
        $first_table
    } else { return [] }
}
#def test-install-apt [] -> table {
#    let table_in: table = $in
#    mut repo_table: table = ($table_in | insert installed $"(ansi red)\(none\)(ansi reset)")
#    let installed_pkgs = ^apt list --installed | lines | parse "{name}/{remainder}"
#    let repo_table_length = $table_in | length
#    let installed_pkgs_length = $installed_pkgs | length
#    
#    for i: int in 0..$repo_table_length {
#        let package = ($repo_table | select $i ).package.0
#        
#        for j in 0..$installed_pkgs_length { 
#            let $inst_pkg = ($installed_pkgs | select $j).name
#            #print $inst_pkg
#            if $package == $inst_pkg { 
#            let policy_table = ^apt-cache policy $package | detect columns --skip 1 --no-headers
#            $repo_table.$i.installed = $"(ansi green)\(($policy_table.column1.0)\)(ansi reset)"
#            break
#        } }
       
            
        
        
#    }

#    repo_table

#}

def prune-search-table [prune_term: string] -> table {
    let input_table: table = $in
    let downcase_prune_term = ($prune_term |str downcase)
    $input_table | filter { |row|    (($row.package | into string | str downcase | str contains $downcase_prune_term) or ($row.description | into string | str downcase | str contains $downcase_prune_term))}
}

def search-pacstall [input: string] -> table {
    if (cmd-exist 'pacstall') {
        let pacstall = do { ^pacstall -S $input  } | complete 
        return ($pacstall.stdout | ansi strip | lines | parse "{package} @ {repo}" | reject repo | insert description '' | insert provider 'pacstall')
      #  if ($result | is-empty) {
      #      print -n $"\e[A\e[K"
       # }
    } else { return [] }
}

def search-flatpak [input: string, desc: bool] -> table {
    if (cmd-exist 'flatpak') {
        if $desc == true {
            let flatpak = (^flatpak search $input --columns=application | lines | wrap 'package' | insert provider 'flatpak' | merge (^flatpak search $input --columns=description | lines | wrap 'description')) 
            if ($flatpak.package.0 == "No matches found") { return [] } else { return $flatpak }
        } else {
            let flatpak = (^flatpak search $input --columns=application | lines | wrap 'package' | insert provider 'flatpak' | insert description '') 
            if ($flatpak.package.0 == "No matches found") { return [] } else { return $flatpak }
        }
    } else {print 'flatpak not installed'; return [] }
}

def search-snap [input: string] -> table {
    if (cmd-exist 'snap') {
        return (^snap search $input | detect columns | get Name | wrap 'package' | insert description '' | insert provider 'snap')
    } else { return [] }
}

#def search-zap [search_term: string] {
#   if (cmd-exist 'zap') {
#    let all_zap_pkgs = ( http get "https://g.srev.in/get-appimage/index.min.json" | select name summary | rename package description | insert provider zap) 
#    $all_zap_pkgs | prune-search-table $search_term 
#   } else { [] }
#}

#USAGE: adds whitespace to the end of a string $in until it is $length characters long
def add-whitespace-until-string-length [length: int] -> string {
    let input: string = $in
    let amount = ($length - ($input | str length --grapheme-clusters))
    mut whitespace_string = $input
    for _ in 0..<$amount {
        $whitespace_string += " "
    }
    $whitespace_string
}

#USAGE: restricts the string to a single line when it prints it
def single-line-print [ input: any = ""] {
    let pipeline: string = ($in | into string) 
    let output :string = ((($pipeline + ($input | into string)) | str replace "\n" " »|« "))
    let deansi_output = ($output | ansi strip)
    let num_of_ansis: int = ($output | split row "\e" | length) - 1
    let ansi_char_difference: int = ($output | str length --grapheme-clusters) - ($deansi_output | str length --grapheme-clusters)

    mut terminal_width = 100;
    if (cmd-exist 'tput') {
        $terminal_width = ((tput cols) | into int)
    } else if (cmd-exist 'stty') {
        $terminal_width = ((stty size| split column " ").column2.0 | into int)
    }
    
    if ($deansi_output | str length ) < ($terminal_width - 2) {
        print $output
    } else {

        (($output | str substring --grapheme-clusters 0..<(($terminal_width - 2) - 3 + $ansi_char_difference - ($num_of_ansis * 2))) + $"(ansi reset)...") | print
    }
    
}
def search [input: string, desc: bool = false, extra_prune_terms: table = []] -> table {
    translation-dir-path | translate searching.apt | print
    # print "Searching apt…"
    let apt = (search-apt $input $desc)
    print -n $"\e[A\e[K"
    # print "Searching Pacstall…"
    translation-dir-path | translate searching.pacstall | print
    let pacstall = (search-pacstall $input)
    print -n $"\e[A\e[K"
    # print "Searching flatpak…"
    translation-dir-path | translate searching.flatpak | print
    let flatpak = (search-flatpak $input $desc)
    print -n $"\e[A\e[K"
    # print "Searching snap…"
    translation-dir-path | translate searching.snap | print
    let snap = (search-snap $input)
    print -n $"\e[A\e[K"
    #print $extra_prune_terms
    mut results =  ($apt | append $pacstall | append $flatpak | append $snap )

    #additional search terms management
    mut search_term: string = $input
    for i in 0..<($extra_prune_terms | length ) {
        let prune_term: string = ($extra_prune_terms | select $i).0
        # prune the results based on other search terms
        $results = ($results | prune-search-table $prune_term)
        # making search terms into one string
        $search_term += " " 
        $search_term += $prune_term
    }


    # print -n $"\e[A\e[K"
    if ($results | is-empty) {
        # print -e $"No packages found matching '($input)'!"
        translation-dir-path | translate none-matching {search: $search_term, color: "magenta" } | print
        exit 1
    }
    let results_len = $results | length

    (translation-dir-path | translate found-matching {matches: $results_len, search: $search_term, color: "magenta"} ) + "\n" | print
   # print $"Found packages matching '(ansi purple_bold)($input)(ansi reset)':\n"

    #preparation for descriptions
    mut longest_package_name_length = 0
    if $desc {
        
        #find length of longest package name
        for i in 0..<$results_len {
            if (($results.package | select $i).0 | str length --grapheme-clusters) > $longest_package_name_length {
                $longest_package_name_length = (($results.package | select $i).0 | str length --grapheme-clusters)
            }
        }
    }
    mut count = 0
    # Loop over results
    for $i in $results {
        let style = match $i.provider {
            "pacstall" => $"(ansi yellow_bold)",
            "apt" => $"(ansi green_bold)",
            "flatpak" => $"(ansi cyan_bold)",
            "snap" => $"(ansi red_bold)",

             _ =>  $"(ansi white_bold)"
        }
        if $desc {
            if ($i.description | is-empty) {
                single-line-print $"[($style)($count)(ansi reset)]:\t($i.package) \(($style)($i.provider)(ansi reset)\)"
            } else {
               
                single-line-print $"[($style)($count)(ansi reset)]:\t($i.package | add-whitespace-until-string-length ($longest_package_name_length + 1))(ansi white_bold)»|«(ansi reset) ($i.description) \(($style)($i.provider)(ansi reset)\)"
            }
        } else {
            single-line-print $"[($style)($count)(ansi reset)]:\t($i.package) \(($style)($i.provider)(ansi reset)\)"
        }
        $count += 1
    }

    return $results
}

def cleanup [promptless: bool = false] {
    if (cmd-exist 'nala') {
        ^sudo nala install --fix-broken
        if $promptless {
            ^sudo nala autoremove -y
        } else {
            ^sudo nala autoremove
        }
    } else {
        ^sudo apt --fix-broken install
        if $promptless { ^sudo apt auto-remove -y } else { ^sudo apt auto-remove }
    }
    if (cmd-exist 'flatpak') {
        ^sudo flatpak repair
        if $promptless { ^sudo flatpak uninstall --unused -y } else { ^sudo flatpak uninstall --unused }
    }
    if (cmd-exist 'snap') {
        let snaps = (^snap list --all | detect columns)
        for $pkg in $snaps {
            if ($pkg.Notes) =~ "disabled" {
                ^sudo snap remove $pkg.Name --revision=$pkg.Rev
            }
        }
    }
}

def update [promptless: bool = false] {
    
    let r_u_sure = translation-dir-path| translate ask.upgrade 
    let sure: string = (input --numchar 1 $"($r_u_sure)")
    # let sure = (input $"Are you sure you want to update all packages? \((ansi green_bold)y(ansi reset)/(ansi red_bold)N(ansi reset)\) ")
    let no: bool = (($sure != "Y") and ($sure != "y"))
    if $no {
        exit 1
    } 
    
    if (cmd-exist 'nala') {
        if $promptless {
            ^sudo nala upgrade --full --no-autoremove -o Acquire::AllowReleaseInfoChange="true" -y
        } else {
            ^sudo nala upgrade --full --no-autoremove -o Acquire::AllowReleaseInfoChange="true"
        }
    } else {
        ^sudo apt update --allow-releaseinfo-change
        if $promptless { ^sudo apt upgrade -y } else { ^sudo apt upgrade }
    }
    if (cmd-exist 'pacstall') {
        ^pacstall -U
        if $promptless { ^pacstall -PUp } else { ^pacstall -Up }
    }
    if (cmd-exist 'flatpak') {
        if $promptless { ^sudo flatpak update -y } else { ^sudo flatpak update }
    }
    if (cmd-exist 'snap') {
        ^sudo snap refresh
    }
    
}



def user-package-selection [ input: table , purpose: string ] -> table {
    mut user_input = ""
    print ""
    let input_final_index = ($input | length) - 1

   $user_input = match $purpose {
        "install" => (input (translation-dir-path| translate ask.which-install {index: $input_final_index}) | into string),
        
        "info" => (input (translation-dir-path |  translate ask.which-info{ index: $input_final_index }) |into string),
        "remove" => (input (translation-dir-path| translate ask.which-remove {index: $input_final_index}) | into string),
        _ => []
    }

    #uses regex to filter out non-number inputs, then converts to int
    mut user_input_ints = ($user_input | split row ' ' | find --regex "[0-9]+" | find --regex "^[0-9]+" | into int) 
    
    #screens the list for invalid indices
    mut drop_list: list<int> = []
    for i in 0..<($user_input_ints | length) {
        if ($user_input_ints |select $i).0 > $input_final_index {
            $drop_list = ($drop_list | append $i)
        }  
    }
    #prunes invalid indices
    for i in 0..<($drop_list|length) {
        $user_input_ints = ($user_input_ints | drop nth ($drop_list| select $i).0)
    }

    #user provided no numbers that were valid
    if ($user_input_ints | is-empty) {
        let error_msg = translation-dir-path | translate invalid.integers {number: $input_final_index}
        error make {msg: $error_msg}
    }
    $user_input_ints
}



def install-or-remove [install: bool = true] {
    let input: table = $in
    let user_input_ints = user-package-selection $input (if $install {"install"} else { "remove" })

    $user_input_ints | each { |index|
        let pkg = ($input | get $index | get package)
        let provider = ($input | get $index | get provider)
        
        
        #print $"Selecting '(ansi purple_bold)($pkg)(ansi reset)' from package manager '(ansi purple_bold)($provider)(ansi reset)'"
        
        if $install {
            translation-dir-path | translate install-select {package: $pkg, manager: $provider, color: "magenta" } | print
        } else {
            translation-dir-path | translate remove-select {package: $pkg, manager: $provider, color: "magenta" } | print
        }


        let r_u_sure = translation-dir-path| translate ask.sure 
        let sure = (input --numchar 1 $"($r_u_sure) ")
        
        let no: bool = (($sure != "Y") and ($sure != "y"))
        # let sure = (input $"Are you sure? \((ansi green_bold)y(ansi reset)/(ansi red_bold)N(ansi reset)\) ")
        if $no {
            translation-dir-path | translate skipping {package: $pkg, manager: $provider, color: "magenta"} | print
        } else {

            if $install {
                match ($provider) {
                    "pacstall" => (^pacstall -I $pkg),
                    "snap" => (^sudo snap install $pkg),
                    "apt" => (^sudo apt install $pkg -y),
                    "flatpak" => (^sudo flatpak install $pkg -y),
                    "zap" =>    (^sudo zap install $pkg -q)
                }
            } else {
                match ($provider) {
                    "pacstall" => (^pacstall -R $pkg),
                    "snap" => (^sudo snap remove $pkg),
                    "apt" => (^sudo apt remove $pkg -y),
                    "flatpak" => (^sudo flatpak remove $pkg -y),
                    "zap" => (^sudo zap remove $pkg -q)
                }
            }
        }
    }
}

def info [ ] {
    let input: table = $in
    let user_input_ints =  user-package-selection $input "info"
}




def search-cmd-no-args [ ] { 
    let command: string = $in
    let error_msg = translation-dir-path | translate invalid.search-arguments {subcommand: $command}
    error make -u {msg: $error_msg}
    exit 1
}



# USAGE: rpk [function] {flag} <input>
# 
# functions:
#     install: Install package(s) - Prompts user to respond with
#              the number(s) associated with the desired package(s).
#              Aliases: nstl, instll, instl, add, i 
#
#     remove:  Uninstall package(s) - Prompts user to respond with
#              the number(s) associated with the desired package(s).
#              Aliases: rm, rmv, uninstall, r
# 
#     search:  Search for package(s) - Does not have a second prompt.
#              Aliases: srch, find, s 
# 
#     update:  Updates all packages accessible to the wrapper - does
#              not accept <input>, instead use install to update
#              individual packages. Has a confirmation prompt.
#              Aliases: upgrade, updt, upd, upg, u
# 
#     cleanup: Attempts to repair broken dependencies and remove any
#              unused packages. Does not accept <input>, but has
#              a confirmation prompt.
#              Aliases: clean, clnp, cln, c
# 
# flags:
#     --help/-h: Display this page
#     
#     --multiterm For commands with package search functionality, switches them to accept multiple arguments as additional filtering on a single search
#
#     --description/-d: By default, rpk will only display packages
#     that contain <input> within their name. Use this flag to increase
#     range and display packages with <input> in their description.
# 
#     -y: Makes functions with confirmation prompts run promptless.
# 
# input:
#     Provide a package name or description.
# 
# Example execution:
#     $ rpk install foobar
#     Found packages matching: 'foobar':
# 
#     [0]: pyfoobar (apt)
#     [1]: foobarshell (apt)
#     [2]: foobar (flatpak)
#     [3]: foobar-web (snap)
#     [4]: foobar-bin (pacstall)
#     [5]: foobar-theme (pacstall)
# 
#     Select which package to install [0-5]: 3 4 5
#     Selecting 'foobar-web' from package manager 'snap'
#     Selecting 'foobar-bin' from package manager 'pacstall'
#     Selecting 'foobar-theme' from package manager 'pacstall'
#     Are you sure? (y/N)
#     [...]
# 
#        .;:;,.  .:
#     'coooooooo:oo.';.
#   ,oooooooooooooooo    ;
#  clllcccllloooooooo;c:'o
# .;';:::::::::cclooooooo'
# ''',::::::::::::::ccclc.
# .''';::::::::::l:::::::
#  '''',:::::::::kd.
#  .''''',;::ck:oW;
#    ''''''''kXOM.
#      .,,:dXMK
#        :k
# 
# rpk 0.1.2
# A package manager wrapper for Pacstall, APT, Flatpak and snap
# Developed by Elsie19 <elsie19@pm.me> for
# the Rhino Linux distribution.
def main [
    --description (-d) # Increase range and display packages with <input> in their description
    --yes (-y) # Makes functions with confirmation prompts run promptless
    --multiterm  # Makes functions with search features use multiple terms, filtering by each term
    ...rest: string # 'install', 'remove', 'search', 'update', 'cleanup'
] -> int {
    if ($rest | is-empty) {
        let error_msg = translation-dir-path | translate invalid.no-subcommand
       error make -u { msg: $error_msg} 
        exit 1
    }
    
    # alias catching
    mut command = match ($rest.0 | str downcase) {
        "install" | "instll" | "instl" | "add" | "nstl" | "i" => "install",
        "remove" | "rmv" | "rm" | "uninstall" | "r" => "remove",
        "search" | "srch" | "find" | "s" => "search",
        "update" | "updt" | "upgrade" | "upd" | "upg" | "u"  => "update",
        "cleanup" | "clnp" | "cln" | "clean" | "c" => "cleanup",
        
        _ => "invalid"
        
   }
# "info" | "information" | "data" | "query" | "qry" | "q" => "info",
    if $command == "invalid" {
        let error_msg = translation-dir-path | translate invalid.subcommand {subcommand: $rest.0}
        error make -u { msg: $error_msg, }
    }

    if $command == "install" {
        if ($rest | length) < 2 { #install was called without a search term
            $command | search-cmd-no-args
        } else {
            if $multiterm {
                (search $rest.1 $description ($rest | skip 2)) | install-or-remove true
            } else {
                ($rest | skip 1) | each { |searchterm|
                    (search $searchterm $description) | install-or-remove true
                }
            }
        }
    }
    if $command == "remove" {
        if ($rest | length) < 2 { #remove was called without a search term
            $command | search-cmd-no-args
        } else {
            if $multiterm {
                (search $rest.1 $description ($rest | skip 2)) | install-or-remove false
            } else {
                ($rest | skip 1) | each { |searchterm|
                    (search $searchterm $description) | install-or-remove false
                }
            }
        }
    }
    if $command == "search" {
        if ($rest | length) < 2 { #search was called without a search term
            $command | search-cmd-no-args
        } else {
            if $multiterm {
                let search = (search $rest.1 $description ($rest | skip 2))
            } else {
                ($rest | skip 1) | each { |searchterm|
                    (search $searchterm $description)
                }
            }
        }
    }

    #currently removed
    if $command == "info" { 
        if ($rest | length) < 2 { #info was called without a search term
        $command | search-cmd-no-args
        } else {
            (search $rest.1 $description ($rest | skip 2)) | info
        }
    }

    match $command {
        "update" => (update $yes),
        "cleanup" => (cleanup $yes)
    }




}

